config {
    type: "operations",
    schema: "Views",
    hasOutput: true,
}

CREATE OR REPLACE MATERIALIZED VIEW
  ${self()} OPTIONS ( max_staleness = INTERVAL "4:0:0" HOUR TO SECOND ) AS (
  SELECT
    FundName,
    FundCode,
    STARTS_WITH(FundName, "1") AS IsOrdinary,
    p.Amount > 0 AS IsPledged,
    c.Amount <= 2500 AS IsCore,
    DATE_TRUNC(GiftDate, MONTH) AS GiftMonth,
    DATE_TRUNC(GiftDate, YEAR) AS GiftYear,
    SUM(ROUND(c.Amount, 2)) AS Total
  FROM
    ${ref("contributions_table")} c
  LEFT JOIN
    ${ref("pledges")} p
  ON
    c.GivingNumber = p.GivingNumber
    AND EXTRACT(YEAR
    FROM
      c.GiftDate) = p.PledgeYear
  GROUP BY
    FundName,
    FundCode,
    IsOrdinary,
    IsPledged,
    IsCore,
    GiftMonth,
    GiftYear );
