config {
    type: "operations",
    hasOutput: true,
    schema: "Models",
    description: "Trains the ARIMA_PLUS model on split donation data"
}

CREATE OR REPLACE MODEL
  ${self()} OPTIONS( model_type = 'ARIMA_PLUS',
    time_series_timestamp_col = 'GiftMonth',
    time_series_data_col = 'Total',
    data_frequency = 'AUTO_FREQUENCY',
    holiday_region = 'US' ) AS
SELECT
  GiftMonth,
  SUM(Total) AS Total
FROM
  ${ref("Views", "monthly_fund_stats")}
WHERE
  IsCore -- restrict to smaller contributions, large is handled by another forecast
  AND IsOrdinary
  AND (IsPledged IS NULL OR NOT IsPledged) -- this is handled by the pledged forecasts
  AND GiftMonth < DATE_TRUNC(CURRENT_DATE(), MONTH)
GROUP BY
  1
