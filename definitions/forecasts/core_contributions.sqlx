config {
    type: "view",
    schema: "Forecasts",
    tags: ["daily"],
    description: "Generates a 12-month forecast for Ordinary and Extraordinary giving"
}

WITH
  Future AS (
    -- 1. FUTURE FORECAST
  SELECT
    DATE(forecast_timestamp) AS forecast_month,
    forecast_value,
    prediction_interval_lower_bound,
    prediction_interval_upper_bound
  FROM
    ML.FORECAST(MODEL ${ref("Models", "core_contribution_forecast")},
      STRUCT(12 AS horizon)) ),
  History AS (
    -- 2. HISTORICAL FITTED VALUES (The Backcast)
  SELECT
    DATE(GiftMonth) AS forecast_month,
    (lower_bound + upper_bound) / 2 AS forecast_value,
    -- What the model thought should happen
    lower_bound AS prediction_interval_lower_bound,
    upper_bound AS prediction_interval_upper_bound
  FROM
    ML.DETECT_ANOMALIES(MODEL ${ref("Models", "core_contribution_forecast")},
      STRUCT(0.95 AS anomaly_prob_threshold)) )
SELECT
  *
FROM
  Future
UNION ALL
SELECT
  *
FROM
  History
ORDER BY
  forecast_month DESC
